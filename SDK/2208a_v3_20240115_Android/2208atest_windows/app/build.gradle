import groovy.xml.Namespace
import groovy.xml.XmlUtil

apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
android {
    compileSdkVersion 33
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    defaultConfig {
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
        flavorDimensions "default"
        applicationId "com.jstyle.test2025"
        minSdkVersion 22
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }
    greendao {
        schemaVersion 4
        daoPackage 'com.jstyle.test2025.dao'
        targetGenDir 'src/main/java'
    }
    repositories {
        flatDir {
            dir 'libs'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }
    productFlavors {



    }
    /**
     * 修改 Android 12 因为 exported 的构建问题
     */

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.processResources.doFirst { pm ->
                String manifestPath = output.processResources.manifestFile
                def manifestFile = new File(manifestPath)
                def xml = new XmlParser(false, true).parse(manifestFile)
                def exportedTag = "android:exported"
                ///指定 space
                def androidSpace = new Namespace('http://schemas.android.com/apk/res/android', 'android')

                def nodes = xml.application[0].'*'.findAll {
                    //挑选要修改的节点，没有指定的 exported 的才需要增加
                    (it.name() == 'activity' || it.name() == 'receiver' || it.name() == 'service') && it.attribute(androidSpace.exported) == null

                }
                ///添加 exported，默认 false
                nodes.each {
                    def isMain = false
                    it.each {
                        if (it.name() == "intent-filter") {
                            it.each {
                                if (it.name() == "action") {
                                    if (it.attributes().get(androidSpace.name) == "android.intent.action.MAIN") {
                                        isMain = true
                                        // println("......................MAIN FOUND......................")
                                    }
                                }
                            }
                        }
                    }
                    it.attributes().put(exportedTag, "${isMain}")
                }

                PrintWriter pw = new PrintWriter(manifestFile)
                pw.write(XmlUtil.serialize(xml))
                pw.close()
            }
        }

    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    testImplementation 'junit:junit:4.12'
    implementation project(':blesdk_2208')
    // compile "com.jstyle:1770sdk:1.0.2"
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.0.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation project(':hellochartslibrary')
    implementation 'org.greenrobot:greendao:3.2.0'
    implementation 'com.google.android:flexbox:1.0.0'
    implementation 'com.github.DeMonLiu623:DeMon-JS-PDF:v1.2'//pdf加载
}
